{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Priority Search Queue\n",
    "###### (https://www.cs.ox.ac.uk/ralf.hinze/publications/ICFP01.pdf)\n",
    "\n",
    "Priority search queues combine the searchability of keys within a balanced binary tree with a heap invariant over each node's priority.\n",
    "\n",
    "We're going to use the outcome of a tournament as an example of how to build a PSQ."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "data PSQ k p = Void\n",
    "             | Winner (k, p) -- ^ The winner's key and priority\n",
    "                      (LTree k p) -- ^ The tree of losers\n",
    "                      k -- ^ the maximum key (used for merging)\n",
    "\n",
    "type Size = Int\n",
    "\n",
    "data LTree k p = Start\n",
    "               | Loser !Size\n",
    "                       (k, p) -- ^ A loser's key and priority\n",
    "                       (LTree k p) -- ^ Left subtree\n",
    "                       k -- ^ the 'split key'\n",
    "                       (LTree k p) -- ^ Right subtree\n",
    "                       \n",
    "-- If the player's priority is less than or equal to the split key then they dominated the left subtree,\n",
    "-- otherwise they dominated the right subtree.\n",
    "\n",
    "key = fst\n",
    "prio = snd\n",
    "\n",
    "maxKey :: PSQ k p -> k\n",
    "maxKey (Winner _ _ m) = m\n",
    "\n",
    "size :: LTree k p -> Size\n",
    "size Start = 0\n",
    "size (Loser s _ _ _ _) = s\n",
    "\n",
    "empty :: PSQ k p\n",
    "empty = Void\n",
    "\n",
    "singleton :: (k, p) -> PSQ k p\n",
    "singleton b = Winner b Start (key b)\n",
    "\n",
    "node :: (k, p) -> LTree k p -> k -> LTree k p -> LTree k p\n",
    "node b l k r = Loser (size l + 1 + size r)\n",
    "                     b l k r\n",
    "\n",
    "-- Merge two queues with the precondition that all keys in the left one are strictly smaller than the keys in the second.\n",
    "merge :: Ord p => PSQ k p -> PSQ k p -> PSQ k p\n",
    "merge Void p = p\n",
    "merge p Void = p\n",
    "merge (Winner b t m) (Winner b' t' m')\n",
    "  | prio b <= prio b' = Winner b (node b' t m t') m'\n",
    "  | otherwise         = Winner b' (node b t m t') m'\n",
    "  \n",
    "fromOrdList :: Ord p => [(k, p)] -> PSQ k p\n",
    "fromOrdList = head . foldm empty . map singleton where\n",
    "  foldm acc [] = [acc]\n",
    "  foldm _ xs = until single group xs\n",
    "  single [_] = True\n",
    "  single _ = False\n",
    "  group (x:y:xs) = x `merge` y : group xs\n",
    "  group xs = xs\n",
    "  \n",
    "popWinner :: (Ord p, Ord k) => PSQ k p -> Maybe ((k, p), PSQ k p)\n",
    "popWinner Void = Nothing\n",
    "popWinner (Winner b t m) = Just (b, secondBest t m)\n",
    "\n",
    "-- Replay the tournament without the champion to get the second best player\n",
    "secondBest :: (Ord p, Ord k) => LTree k p -> k -> PSQ k p\n",
    "secondBest Start m = Void\n",
    "secondBest (Loser _ b t k u) m\n",
    "  | key b <= k = Winner b t k `merge` secondBest u m\n",
    "  | otherwise = secondBest t k `merge` Winner b u m\n",
    "  \n",
    "toOrdList :: (Ord k) => PSQ k p -> [(k, p)]\n",
    "toOrdList Void = []\n",
    "toOrdList (Winner b t m) = ($ []) $ trav b t where\n",
    "  trav b Start = (b :)\n",
    "  trav b (Loser _ b' tl k tr)\n",
    "    | key b' <= k = trav b' tl . trav b tr\n",
    "    | otherwise   = trav b tl . trav b' tr\n",
    "    \n",
    "data TreeView k p\n",
    "  = Z\n",
    "  | S (k, p)\n",
    "  | M (PSQ k p) (PSQ k p) -- all keys in left are strictly less than those in right\n",
    "    \n",
    "treeView :: Ord k => PSQ k p -> TreeView k p\n",
    "treeView Void = Z\n",
    "treeView (Winner b Start _) = S b\n",
    "treeView (Winner b (Loser _ b' tl sk tr) m)\n",
    "  | key b' <= sk = M (Winner b' tl sk) (Winner b tr m)\n",
    "  | otherwise    = M (Winner b tl sk) (Winner b' tr m)\n",
    "\n",
    "lookup :: Ord k => k -> PSQ k p -> Maybe p\n",
    "lookup k psq = case treeView psq of\n",
    "  Z -> Nothing\n",
    "  S b -> if k == key b then Just (prio b) else Nothing\n",
    "  M tl tr\n",
    "    | k <= maxKey tl -> lookup k tl\n",
    "    | otherwise -> lookup k tr\n",
    "\n",
    "adjust :: (Ord k, Ord p) => (p -> p) -> k -> PSQ k p -> PSQ k p\n",
    "adjust f k psq = case treeView psq of\n",
    "  Z -> Void\n",
    "  S b -> if k == key b then singleton (k, f $ prio b)\n",
    "                       else singleton b\n",
    "  M tl tr\n",
    "    | k <= maxKey tl -> adjust f k tl `merge` tr\n",
    "    | otherwise -> tl `merge` adjust f k tr\n",
    "    \n",
    "insert :: (Ord k, Ord p) => (k, p) -> PSQ k p -> PSQ k p\n",
    "insert b psq = case treeView psq of\n",
    "  Z -> singleton b\n",
    "  S b'\n",
    "    | key b < key b' -> singleton b `merge` singleton b'\n",
    "    | key b == key b' -> singleton b\n",
    "    | otherwise -> singleton b' `merge` singleton b\n",
    "  M tl tr\n",
    "    | key b <= maxKey tl -> insert b tl `merge` tr\n",
    "    | otherwise -> tl `merge` insert b tr\n",
    "    \n",
    "delete :: (Ord k, Ord p) => k -> PSQ k p -> PSQ k p\n",
    "delete k psq = case treeView psq of\n",
    "  Z -> Void\n",
    "  S b\n",
    "    | k == key b -> Void\n",
    "    | otherwise -> singleton b\n",
    "  M tl tr\n",
    "    | k <= maxKey tl -> delete k tl `merge` tr\n",
    "    | otherwise -> tl `merge` delete k tr"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### A Balanced Scheme\n",
    "\n",
    "Priority search queues can be augmented with a balancing scheme to improve the situation with operations such as `insert` which otherwise could result in degenerate trees.\n",
    "\n",
    "For the scheme we will use, a tree is weight-balanced if for all nodes either both subtrees have at most one element or one subtree does not have more than `w` times as many elements as it's sibling, where `w` is some constant > 3.75. To check and maintain this invariant, each node in a loser tree has it's size cached on it's root node."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [],
   "source": [
    "w :: Int\n",
    "w = 4\n",
    "\n",
    "balance :: (Ord k, Ord p) => (k, p) -> LTree k p -> k -> LTree k p -> LTree k p\n",
    "balance b l k r\n",
    "  | size l + size r < 2 = node b l k r\n",
    "  | size r > w * size l = balanceLeft b l k r\n",
    "  | size l > w * size r = balanceRight b l k r\n",
    "  | otherwise           = node b l k r\n",
    "\n",
    "balanceLeft :: (Ord k, Ord p) => (k, p) -> LTree k p -> k -> LTree k p -> LTree k p\n",
    "balanceLeft b l k r@(Loser _ _ rl _ rr)\n",
    "  | size rl < size rr = singleLeft b l k r\n",
    "  | otherwise         = doubleLeft b l k r\n",
    "\n",
    "balanceRight :: (Ord k, Ord p) => (k, p) -> LTree k p -> k -> LTree k p -> LTree k p\n",
    "balanceRight b l@(Loser _ _ ll _ lr) k r\n",
    "  | size lr < size ll = singleRight b l k r\n",
    "  | otherwise         = doubleRight b l k r\n",
    "  \n",
    "singleLeft :: (Ord k, Ord p) => (k, p) -> LTree k p -> k -> LTree k p -> LTree k p\n",
    "singleLeft b1 t1 k1 (Loser _ b2 t2 k2 t3)\n",
    "  | key b2 <= k2 && prio b1 <= prio b2\n",
    "  = node b1 (node b2 t1 k1 t2) k2 t3\n",
    "  | otherwise = node b2 (node b1 t1 k1 t2) k2 t3\n",
    "\n",
    "singleRight :: (Ord k, Ord p) => (k, p) -> LTree k p -> k -> LTree k p -> LTree k p\n",
    "singleRight b1 (Loser _ b2 t1 k1 t2) k2 t3\n",
    "  | key b2 > k1 && prio b1 <= prio b2\n",
    "  = node b1 t2 k1 (node b2 t2 k2 t3)\n",
    "  | otherwise = node b2 t1 k1 (node b1 t2 k2 t3)\n",
    "  \n",
    "doubleLeft :: (Ord k, Ord p) => (k, p) -> LTree k p -> k -> LTree k p -> LTree k p\n",
    "doubleLeft b1 t1 k1 (Loser _ b2 t2 k2 t3)\n",
    "  = singleLeft b1 t1 k1 (singleRight b2 t2 k2 t3)\n",
    "\n",
    "doubleRight :: (Ord k, Ord p) => (k, p) -> LTree k p -> k -> LTree k p -> LTree k p\n",
    "doubleRight b1 (Loser _ b2 t1 k1 t2) k2 t3\n",
    "  = singleRight b1 (singleLeft b2 t1 k1 t2) k2 t3"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "To adapt the dictionary operations to use balancing, occurences of `Loser` should be replaced with `balance`. If the operation does not change the shape of the tree (such as `adjust`), then `node` can be used."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "name": "haskell",
   "pygments_lexer": "Haskell",
   "version": "8.6.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
