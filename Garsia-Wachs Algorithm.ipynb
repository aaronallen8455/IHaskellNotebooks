{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Garsia-Wachs Algorithm\n",
    "###### (Algorithm Design with Haskell)\n",
    "\n",
    "Garsia-Wachs is an algorithm for finding a binary tree of minimum cost with a fringe equal to the input. It works in two stages\n",
    "- Build a tree from a list of weights that has as it's labels the indices [1..n] of the input. The fringe is not necessarily equal to the input.\n",
    "- Using the depths of the nodes in the first tree, build a second tree by bracketing the input weights such that their corresponding depth in the first tree must match the depth of it's pair. This first tree is constructed in such a way that the resulting tree is the optimal one with the input as it's fringe.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "type Weight = Int\n",
    "type Label = Int\n",
    "type Depth = Int\n",
    "data Tree a = Leaf a | Fork (Tree a) (Tree a)\n",
    "\n",
    "gwa :: [Weight] -> Tree Weight\n",
    "gwa ws = rebuild ws (build ws)\n",
    "\n",
    "build :: [Weight] -> Tree Label\n",
    "build = undefined\n",
    "\n",
    "rebuild :: [Weight] -> Tree Label -> Tree Weight\n",
    "rebuild = undefined"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Once we have the output of `build`, the next step is to break it into a list of pairs where the first elem is a leaf with the weight as it's label and the second elem being it's depth in the first tree. This list is arranged in fringe order.\n",
    "\n",
    "We want to repeatedly combine adjacent pairs who's depths are the same until a single tree remains. When two trees are combined, the depth is reduced by 1."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [],
   "source": [
    "depth = snd\n",
    "\n",
    "reduce :: [(Tree Weight, Depth)] -> Tree Weight\n",
    "reduce = extract . until single step where\n",
    "  extract [(t,_)] = t\n",
    "  single [x] = True\n",
    "  single _ = False\n",
    "  step (x:y:xs) = if depth x == depth y\n",
    "                      then join x y : xs\n",
    "                      else x : step (y : xs)\n",
    "join (t1, d) (t2, _) = (Fork t1 t2, d - 1)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "This implementation is inefficient because at each step it starts from the beginning of the list even though we may have already discovered that the front of the list cannot be reduced further. We can fix this using a left fold and recursive definition of step."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [],
   "source": [
    ":m Data.List\n",
    "\n",
    "reduce :: [(Tree Weight, Depth)] -> Tree Weight\n",
    "reduce = extract . foldl' step [] where\n",
    "  extract [(t,_)] = t\n",
    "  step [] y = [y]\n",
    "  step (x:xs) y = if depth x == depth y\n",
    "                    then step xs (join x y)\n",
    "                    else y:x:xs"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now to implement `rebuild`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [],
   "source": [
    "import qualified Data.Array as A\n",
    "\n",
    "rebuild :: [Weight] -> Tree Label -> Tree Weight\n",
    "rebuild ws = reduce . zip (map Leaf ws) . sortDepths (length ws)\n",
    "\n",
    "fringeDepth :: Tree a -> [(a, Depth)]\n",
    "fringeDepth = ($ []) . from 0 where\n",
    "  from d (Leaf a) = ((a, d) :)\n",
    "  from d (Fork t1 t2) = from (d + 1) t1 . from (d + 1) t2\n",
    "\n",
    "sortDepths :: Int -> Tree Label -> [Depth]\n",
    "sortDepths size t = A.elems $ A.array (1,  size) (fringeDepth t)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now we need the first stage of the algorithm, `build`, which is the complicated part. We start with a quadratic implementation and then move to a linearithmic one.\n",
    "\n",
    "Begin with a list of `[(0, w0), (1, w1), (2, w2)...]` where the first elem of each pair is a Leaf and the second is a weight. The first pair is a sentinel and has infinite weigth.\n",
    "\n",
    "At each step we find the pair with the largest index such that the weight of it's left neighbor is >= the weight of it's right neighbor. Combine this pair with it's right neighbor so that the trees are joined and the weights are added, then move this new pair to the right of all pairs which have a lesser weight.\n",
    "\n",
    "At the end of this process there are just two pairs left, the sentinel and the pair whose tree is the desired result.\n",
    "\n",
    "Once a pair has been selected, merged, and moved into place, the possible choices for the next pair is limited to\n",
    "- the pair to the right of where the first pair got moved, if it's right neighbor has weight less than the moved pair's.\n",
    "- the pair to the right of where the moved pair used to be if it's neighbors fit the condition.\n",
    "- any of the pairs to the left of the left neighbor of where the moved pair used to be."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [],
   "source": [
    "build :: [Weight] -> Tree Label\n",
    "build = extract\n",
    "      . foldr step []\n",
    "      . (zip (Leaf <$> [0..]))\n",
    "      . (maxBound :) -- could use sum of weights instead of maxBound\n",
    "  where\n",
    "    extract [(t,_), _] = t\n",
    "\n",
    "    step x (y:z:zs)\n",
    "      | weight x >= weight z = step x $ move (join y z) zs\n",
    "    step x xs = x : xs\n",
    "\n",
    "    move x [] = [x]\n",
    "    move x (y:ys)\n",
    "      | weight x > weight y = y : move x ys\n",
    "      | otherwise = step x (y : ys)\n",
    "    \n",
    "    weight = snd\n",
    "join (t1, w1) (t2, w2) = (Fork t1 t2, w1 + w2)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "In the worst case `build` is quadratic. To improve this we will exploit the fact that the second argument to both step and move are \"2-sorted\", meaning A1,A3,A5 and A2,A4,A6 are sorted lists in terms of weight. This will allow for a linear implementation and bring the overall runtime of the algorithm to linearithmic.\n",
    "\n",
    "To do this we will use a data structure that supports efficiently splitting at some value, which is what we need for `move`. We'll use a binary search tree where a flatenning of the tree produces a 2-sorted list. In order to insert we need to look at both a nodes weight and the weight of the preceding node to determine whether to go right or left. To this end, we will install the previous weight on each node."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 22,
   "metadata": {},
   "outputs": [],
   "source": [
    "data List a = Null\n",
    "            | Node Int (List a) (a, a) (List a)\n",
    "            \n",
    "height :: List a -> Int\n",
    "height Null = 0\n",
    "height (Node h _ _ _) = h\n",
    "            \n",
    "emptyL :: List a\n",
    "emptyL = Null\n",
    "nullL :: List a -> Bool\n",
    "nullL Null = True\n",
    "nullL _ = False\n",
    "\n",
    "node :: List a -> (a,a) -> List a -> List a\n",
    "node t1 x t2 = Node (max (height t1) (height t2) + 1) t1 x t2\n",
    "\n",
    "bias :: List a -> Int\n",
    "bias Null = 0\n",
    "bias (Node _ l _ r) = height l - height r\n",
    "\n",
    "balance :: List a -> (a,a) -> List a -> List a\n",
    "balance t1 x t2\n",
    "  | abs (h1 - h2) <= 1 = node t1 x t2\n",
    "  | h1 == h2 + 2 = rotateR t1 x t2\n",
    "  | h2 == h1 + 2 = rotateL t1 x t2\n",
    "  where\n",
    "    h1 = height t1; h2 = height t2\n",
    "rotateR t1 x t2\n",
    "  | 0 <= bias t1 = rotr (node t1 x t2)\n",
    "  | otherwise = rotr (node (rotl t1) x t2)\n",
    "rotateL t1 x t2\n",
    "  | bias t2 <= 0 = rotl (node t1 x t2)\n",
    "  | otherwise = rotl (node t1 x (rotr t2))\n",
    "rotr (Node _ (Node _ l y lr) x r) = node l y (node lr x r)\n",
    "rotl (Node _ l x (Node _ rl y r)) = node (node l x rl) y r\n",
    "\n",
    "balanceR :: List a -> (a,a) -> List a -> List a\n",
    "balanceR (Node _ l y r) x t2\n",
    "  | height r >= height t2 = balance l y (balanceR r x t2)\n",
    "  | otherwise = balance l y (node r x t2)\n",
    "  \n",
    "balanceL :: List a -> (a,a) -> List a -> List a\n",
    "balanceL t2 x (Node _ l y r)\n",
    "  | height l >= height t2 = balance (balanceL l x t2) y r\n",
    "  | otherwise = balance (node l x t2) y r\n",
    "\n",
    "gbalance :: List a -> (a, a) -> List a -> List a\n",
    "gbalance l x r\n",
    "  | abs (hl - hr) <= 2 = balance l x r\n",
    "  | hl > hr + 2 = balanceR l x r\n",
    "  | hl + 2 < hr = balanceL l x r\n",
    "  where hl = height l; hr = height r\n",
    "\n",
    "consL :: a -> List a -> List a\n",
    "consL x Null = node Null (x, x) Null\n",
    "consL x (Node _ t1 (y, z) t2)\n",
    "  | nullL t1 = balance (consL x t1) (x, z) t2\n",
    "  | otherwise = balance (consL x t1) (y, z) t2\n",
    "  \n",
    "deconsL :: List a -> (a, List a)\n",
    "deconsL (Node _ l (x, y) r)\n",
    "  | nullL l = (y, r)\n",
    "  | otherwise = (z, balance t (x, y) r)\n",
    "  where\n",
    "    (z, t) = deconsL l\n",
    "    \n",
    "lastL :: List a -> a\n",
    "lastL (Node _ t1 (x,y) t2) = if nullL t2 then y else lastL t2\n",
    "    \n",
    "concatL :: List a -> List a -> List a\n",
    "concatL t Null = t\n",
    "concatL Null t = t\n",
    "concatL t1 t2 = gbalance t1 (x, y) t2\n",
    "  where x = lastL t1\n",
    "        (y, t3) = deconsL t2\n",
    "        \n",
    "type Pair = (Tree Label, Weight)\n",
    "weight :: Pair -> Weight\n",
    "weight (t, w) = w\n",
    "        \n",
    "splitL :: Pair -> List Pair -> (List Pair, List Pair)\n",
    "splitL x t = sew (pieces x t)\n",
    "\n",
    "data Piece a = LP (List a) (a, a)\n",
    "             | RP (a, a) (List a)\n",
    "             \n",
    "pieces :: Pair -> List Pair -> [Piece Pair]\n",
    "pieces x t = addPiece t [] where\n",
    "  addPiece Null ps = ps\n",
    "  addPiece (Node _ t1 (y,z) t2) ps\n",
    "    | weight x > max (weight y) (weight z)\n",
    "    = addPiece t2 (LP t1 (y,z) : ps)\n",
    "    | otherwise = addPiece t1 (RP (y,z) t2 : ps)\n",
    "    \n",
    "sew :: [Piece Pair] -> (List Pair, List Pair)\n",
    "sew = foldl' go (emptyL, emptyL) where\n",
    "  go (t1, t2) (LP t x) = (gbalance t x t1, t2)\n",
    "  go (t1, t2) (RP x t) = (t1, gbalance t2 x t)\n",
    "  \n",
    "-- now we can define it!\n",
    "buildL :: [Weight] -> Tree Label\n",
    "buildL ws = extractL (foldr stepL emptyL (start ws)) where\n",
    "  start ws = zip (map Leaf [0..]) (maxBound : ws)\n",
    "  \n",
    "  extractL xs = t where\n",
    "    (_, ys) = deconsL xs\n",
    "    ((t, _), _) = deconsL ys\n",
    "    \n",
    "  stepL :: Pair -> List Pair -> List Pair\n",
    "  stepL x xs\n",
    "    | nullL xs || nullL ys || weight x < weight z\n",
    "    = consL x xs\n",
    "    | otherwise = stepL x (insertL (join y z) zs)\n",
    "    where\n",
    "      (y, ys) = deconsL xs\n",
    "      (z, zs) = deconsL ys\n",
    "      \n",
    "  insertL :: Pair -> List Pair -> List Pair\n",
    "  insertL x xs = concatL ys (stepL x zs) where\n",
    "    (ys, zs) = splitL x xs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "name": "haskell",
   "pygments_lexer": "Haskell",
   "version": "8.6.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
