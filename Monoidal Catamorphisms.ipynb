{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "remarkable-secret",
   "metadata": {},
   "source": [
    "# Monoidal Catamorphisms\n",
    "###### (https://bartoszmilewski.com/2020/06/15/monoidal-catamorphisms/)\n",
    "\n",
    "If you have a container of values from some monoid, you may want to summarize the entire structure by accumulating all the values into a single result, then convert that to some output type."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "reverse-military",
   "metadata": {},
   "outputs": [],
   "source": [
    ":ext ExistentialQuantification\n",
    "\n",
    "import Data.Bifunctor\n",
    "\n",
    "data Fold a b = forall m. Monoid m => Fold (a -> m) (m -> b)\n",
    "\n",
    "-- it's simple to express this as a computation on lists\n",
    "fold :: Fold a b -> [a] -> b\n",
    "fold (Fold s g) = g . foldMap s\n",
    "\n",
    "instance Functor (Fold a) where\n",
    "  fmap f (Fold scatter gather) = Fold scatter (f . gather)\n",
    "  \n",
    "-- it's a monoidal functor as well, which we can express as an instance of Applicative\n",
    "instance Applicative (Fold a) where\n",
    "  pure a = Fold (const ()) (const a)\n",
    "  Fold s1 g1 <*> Fold s2 g2 = Fold (\\a -> (s1 a, s2 a)) (\\(m1, m2) -> (g1 m1) (g2 m2))\n",
    "  \n",
    "class Monoidal f where\n",
    "  init :: f ()\n",
    "  combine :: f a -> f b -> f (a, b)\n",
    "  \n",
    "instance Monoidal (Fold a) where\n",
    "  init = Fold bang id\n",
    "  combine (Fold s g) (Fold s' g') = Fold (tuple s s') (bimap g g')\n",
    "  \n",
    "bang :: a -> ()\n",
    "bang _ = ()\n",
    "\n",
    "tuple :: (c -> a) -> (c -> b) -> (c -> (a, b))\n",
    "tuple f g = (,) <$> f <*> g"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cultural-syria",
   "metadata": {},
   "source": [
    "An algebra takes the contents of something and summarizes it"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "copyrighted-lying",
   "metadata": {},
   "outputs": [],
   "source": [
    "type Algebra f a = f a -> a\n",
    "\n",
    "-- A fixed point of a functor is the carrier of it's initial algebra\n",
    "newtype Fix f = Fix { unFix :: f (Fix f) }\n",
    "\n",
    "-- catamorphisms generalize folds\n",
    "cata :: Functor f => Algebra f a -> Fix f -> a\n",
    "cata alg = alg . fmap (cata alg) . unFix"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "binding-basketball",
   "metadata": {},
   "source": [
    "### Monoidal algebras"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "supported-enlargement",
   "metadata": {},
   "source": [
    "Our goal is use `Fold` to fold an arbitrary recursive data structure. Such structures are generated by functors of two arguments (bifunctors).\n",
    "\n",
    "The first argument will be the payload and the second, the placeholder for recursion and carrier for the algebra.\n",
    "\n",
    "We can define a monoidal algebra for such a functor by assuming that it has a monoidal payload with the child nodes having been already evaluated to a monoidal value."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 27,
   "id": "baking-moisture",
   "metadata": {},
   "outputs": [],
   "source": [
    ":ext RankNTypes\n",
    "\n",
    "type MAlgebra f = forall m. Monoid m => f m m -> m\n",
    "\n",
    "cat :: (Functor (f a), Bifunctor f) => MAlgebra f -> Fold a b -> Fix (f a) -> b\n",
    "cat malg (Fold s g) = g . cata alg where\n",
    "  alg = malg . bimap s id"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "extreme-stopping",
   "metadata": {},
   "source": [
    "### Example"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 33,
   "id": "functional-duplicate",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\"13\""
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    ":ext DeriveFunctor\n",
    "import Data.Monoid\n",
    "\n",
    "data TreeF a r = Leaf a | Node r r deriving Functor\n",
    "\n",
    "instance Bifunctor TreeF where\n",
    "  bimap f _ (Leaf a) = Leaf $ f a\n",
    "  bimap _ g (Node r r') = Node (g r) (g r')\n",
    "  \n",
    "type Tree a = Fix (TreeF a)\n",
    "\n",
    "leaf :: a -> Tree a\n",
    "leaf a = Fix (Leaf a)\n",
    "\n",
    "node :: Tree a -> Tree a -> Tree a\n",
    "node t t' = Fix (Node t t')\n",
    "\n",
    "myAlg :: MAlgebra TreeF\n",
    "myAlg (Leaf m) = m\n",
    "myAlg (Node m m') = m <> m'\n",
    "\n",
    "myFold :: Fold Double String\n",
    "myFold = Fold floor' show' where\n",
    "  floor' :: Double -> Sum Int\n",
    "  floor' = Sum . floor\n",
    "  show' :: Sum Int -> String\n",
    "  show' = show . getSum\n",
    "  \n",
    "-- This fold has no knowledge of the data structure, it's only interested in the payload.\n",
    "\n",
    "myTree :: Tree Double\n",
    "myTree = node (node (leaf 2.3) (leaf 10.3)) (leaf 1.1)\n",
    "\n",
    "cat myAlg myFold myTree"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "progressive-repeat",
   "metadata": {},
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "mimetype": "text/x-haskell",
   "name": "haskell",
   "pygments_lexer": "Haskell",
   "version": "8.8.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
