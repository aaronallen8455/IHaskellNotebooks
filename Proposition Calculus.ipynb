{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "alike-myanmar",
   "metadata": {},
   "source": [
    "# Proposition Calculus\n",
    "###### (The Science of Programming, Chapter 2)\n",
    "\n",
    "Proving the fundamentals of boolean logic propositions."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 84,
   "id": "exceptional-opportunity",
   "metadata": {},
   "outputs": [],
   "source": [
    ":set -W\n",
    ":set -Wno-name-shadowing\n",
    ":set -Werror\n",
    ":ext GADTs\n",
    ":ext DataKinds\n",
    ":ext TypeOperators\n",
    ":ext KindSignatures\n",
    ":ext PolyKinds\n",
    "\n",
    "import Control.Category\n",
    "import Data.Proxy\n",
    "import Data.Type.Bool\n",
    "import Data.Type.Equality\n",
    "import Prelude hiding ((.))\n",
    "\n",
    "data SBool (b :: Bool) where\n",
    "  STrue :: SBool 'True\n",
    "  SFalse :: SBool 'False\n",
    "\n",
    "sNot :: Not a ~ b => SBool a -> SBool b\n",
    "sNot STrue = SFalse\n",
    "sNot SFalse = STrue\n",
    "\n",
    "-- implication\n",
    "type (a :: Bool) :=> (b :: Bool) = Not a || b\n",
    "\n",
    "type Tautology a = a :~: 'True\n",
    "\n",
    "-- Theorems\n",
    "\n",
    "commAnd :: SBool a -> SBool b -> (a && b) :~: (b && a)\n",
    "commAnd STrue _ = Refl\n",
    "commAnd SFalse _ = Refl\n",
    "\n",
    "commOr :: SBool a -> SBool b -> (a || b) :~: (b || a)\n",
    "commOr STrue _ = Refl\n",
    "commOr SFalse _ = Refl\n",
    "\n",
    "assocAnd :: SBool a -> SBool b -> SBool c\n",
    "         -> (a && (b && c)) :~: ((a && b) && c)\n",
    "assocAnd STrue _ _ = Refl\n",
    "assocAnd SFalse _ _ = Refl\n",
    "\n",
    "assocOr :: SBool a -> SBool b -> SBool c\n",
    "        -> (a || (b || c)) :~: ((a || b) || c)\n",
    "assocOr STrue _ _ = Refl\n",
    "assocOr SFalse _ _ = Refl\n",
    "\n",
    "distAnd :: SBool a -> SBool b -> SBool c\n",
    "        -> (a && (b || c)) :~: ((a && b) || (a && c))\n",
    "distAnd STrue _ _ = Refl\n",
    "distAnd SFalse _ _ = Refl\n",
    "\n",
    "distOr :: SBool a -> SBool b -> SBool c\n",
    "       -> (a || (b && c)) :~: ((a || b) && (a || c))\n",
    "distOr STrue _ _ = Refl\n",
    "distOr SFalse _ _ = Refl\n",
    "\n",
    "deMorganAnd :: SBool a -> SBool b\n",
    "            -> Not (a && b) :~: (Not a || Not b)\n",
    "deMorganAnd STrue _ = Refl\n",
    "deMorganAnd SFalse _ = Refl\n",
    "\n",
    "deMorganOr :: SBool a -> SBool b\n",
    "           -> Not (a || b) :~: (Not a && Not b)\n",
    "deMorganOr STrue _ = Refl\n",
    "deMorganOr SFalse _ = Refl\n",
    "\n",
    "doubleNeg :: SBool a -> Not (Not a) :~: a\n",
    "doubleNeg STrue = Refl\n",
    "doubleNeg SFalse = Refl\n",
    "\n",
    "exclMid :: SBool a -> (Not a || a) :~: 'True\n",
    "exclMid STrue = Refl\n",
    "exclMid SFalse = Refl\n",
    "\n",
    "contradiction :: SBool a -> (a && Not a) :~: 'False\n",
    "contradiction STrue = Refl\n",
    "contradiction SFalse = Refl\n",
    "\n",
    "equality :: SBool a -> SBool b\n",
    "         -> a :~: b\n",
    "         -> ((a :=> b) && (b :=> a)) :~: 'True\n",
    "equality STrue _ Refl = Refl\n",
    "equality SFalse _ Refl = Refl\n",
    "\n",
    "conjunctive :: SBool a -> SBool b\n",
    "            -> (a || b) :~: Not (Not a && Not b)\n",
    "conjunctive STrue _ = Refl\n",
    "conjunctive SFalse b =\n",
    "  case doubleNeg b of\n",
    "    Refl -> Refl\n",
    "    \n",
    "disjunctive :: SBool a -> SBool b\n",
    "            -> (a && b) :~: Not (Not a || Not b)\n",
    "disjunctive STrue b =\n",
    "  case doubleNeg b of\n",
    "   Refl -> Refl\n",
    "disjunctive SFalse _ = Refl\n",
    "\n",
    "-- Lemmas\n",
    "\n",
    "lemma1 :: SBool a\n",
    "       -> Not (a && Not a) :~: 'True\n",
    "lemma1 STrue = Refl\n",
    "lemma1 SFalse = Refl\n",
    "\n",
    "lemma2 :: SBool b -> SBool c\n",
    "       -> ((b && (b :=> c)) :=> c) :~: 'True\n",
    "lemma2 STrue c = exclMid c\n",
    "lemma2 SFalse _ = Refl\n",
    "\n",
    "lemma3 :: SBool a -> SBool b -> SBool c -> SBool d\n",
    "       -> ((a && b && c) :=> d)\n",
    "      :~: (Not a || Not b || Not c || d)\n",
    "lemma3 STrue b c d =\n",
    "  case deMorganAnd b c of\n",
    "    Refl -> sym (assocOr (sNot b) (sNot c) d)\n",
    "lemma3 SFalse _ _ _ = Refl\n",
    "\n",
    "lemma4 :: SBool b -> SBool c -> SBool d\n",
    "       -> ((Not (b :=> c) && Not (Not b :=> (c || d))) :=> (Not c :=> d))\n",
    "     :~: 'True\n",
    "lemma4 STrue _ _ = Refl\n",
    "lemma4 SFalse _ _ = Refl\n",
    "\n",
    "lemma5 :: SBool x -> SBool y\n",
    "       -> (x || ( y || x) || Not y) :~: 'True\n",
    "lemma5 STrue _ = Refl\n",
    "lemma5 SFalse y =\n",
    "  case commOr (sNot y) y of\n",
    "    Refl -> exclMid y\n",
    "    \n",
    "lemma6 :: SBool x -> SBool y\n",
    "       -> ((x || y) && (x || Not y)) :~: x\n",
    "lemma6 STrue _ = Refl\n",
    "lemma6 SFalse y = contradiction y\n",
    "\n",
    "lemma7 :: SBool x -> SBool y\n",
    "       -> (x || y || Not x) :~: 'True\n",
    "lemma7 STrue _ = Refl\n",
    "lemma7 SFalse _ = Refl\n",
    "\n",
    "lemma8 :: SBool x -> SBool y\n",
    "       -> ((x || y) && (x || Not y) && (Not x || y) && (Not x || Not y)) :~: 'False\n",
    "lemma8 STrue = contradiction\n",
    "lemma8 SFalse = contradiction\n",
    "\n",
    "lemma9 :: SBool x -> SBool y\n",
    "       -> ((x && y) || (x && Not y) || (Not x && y) || (Not x && Not y)) :~: 'True\n",
    "lemma9 STrue y =\n",
    "  case commOr (sNot y) y of\n",
    "    Refl -> exclMid y\n",
    "lemma9 SFalse y =\n",
    "  case commOr (sNot y) y of\n",
    "    Refl -> exclMid y\n",
    "\n",
    "lemma10 :: SBool x -> SBool y\n",
    "        -> ((Not x && y) || x) :~: (x || y)\n",
    "lemma10 STrue _ = Refl\n",
    "lemma10 SFalse _ = Refl\n",
    "\n",
    "lemma11 :: SBool x -> SBool y\n",
    "        -> (Not x :=> (x && y)) :~: x\n",
    "lemma11 STrue _ = Refl\n",
    "lemma11 SFalse _ = Refl\n",
    "\n",
    "lemma12 :: SBool x\n",
    "        -> ('True :=> (Not x :=> x)) :~: x\n",
    "lemma12 STrue = Refl\n",
    "lemma12 SFalse = Refl\n",
    "\n",
    "lemma13 :: SBool x -> SBool y\n",
    "        -> (x :=> (y :=> (x && y))) :~: 'True\n",
    "lemma13 STrue y = exclMid y\n",
    "lemma13 SFalse _ = Refl\n",
    "\n",
    "lemma14 :: SBool x -> SBool y\n",
    "        -> (Not x :=> (Not x :=> (Not x && y)))\n",
    "       :~: (x || y)\n",
    "lemma14 STrue _ = Refl\n",
    "lemma14 SFalse _ = Refl\n",
    "\n",
    "lemma15 :: SBool y\n",
    "        -> (Not y :=> y) :~: y\n",
    "lemma15 STrue = Refl\n",
    "lemma15 SFalse = Refl\n",
    "\n",
    "lemma16 :: SBool y\n",
    "        -> (Not y :=> Not y) :~: 'True\n",
    "lemma16 STrue = Refl\n",
    "lemma16 SFalse = Refl"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "helpful-basement",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Haskell",
   "language": "haskell",
   "name": "haskell"
  },
  "language_info": {
   "codemirror_mode": "ihaskell",
   "file_extension": ".hs",
   "mimetype": "text/x-haskell",
   "name": "haskell",
   "pygments_lexer": "Haskell",
   "version": "8.8.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
